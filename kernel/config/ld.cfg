/*
============================================================================
  Este arquivo pertence ao Projeto do Sistema Operacional LuckyOS (LOS).
  --------------------------------------------------------------------------
  Copyright (C) 2013 - Luciano L. Goncalez
  --------------------------------------------------------------------------
  a.k.a.: Master Lucky
  eMail : master.lucky.br@gmail.com
  Home  : http://lucky-labs.blogspot.com.br
  ==========================================================================
  Este programa e software livre; voce pode redistribui-lo e/ou modifica-lo
  sob os termos da Licenca Publica Geral GNU, conforme publicada pela Free
  Software Foundation; na versao 2 da Licenca.

  Este programa e distribuido na expectativa de ser util, mas SEM QUALQUER
  GARANTIA; sem mesmo a garantia implicita de COMERCIALIZACAO ou de
  ADEQUACAO A QUALQUER PROPOSITO EM PARTICULAR. Consulte a Licenca Publica
  Geral GNU para obter mais detalhes.

  Voce deve ter recebido uma copia da Licenca Publica Geral GNU junto com
  este programa; se nao, escreva para a Free Software Foundation, Inc., 59
  Temple Place, Suite 330, Boston, MA 02111-1307, USA. Ou acesse o site do
  GNU e obtenha sua licenca: http://www.gnu.org/
  ==========================================================================
  ld.cfg
  --------------------------------------------------------------------------
    Este eh um arquivo de configuracao do linkeditor, ele eh responsavel
  pela correta montagem do kernel na sua imagem binÃ¡ria.
  --------------------------------------------------------------------------
  Versao: 0.3
  Data: 22/09/2013
  --------------------------------------------------------------------------
  Uso: Passado como parametro ao LD
  > ld -T ld.cfg
  ------------------------------------------------------------------------
  Executar: Arquivo de configuracao.
============================================================================
*/

/*
  Opcoes que podem ser uteis posteriormente

  INPUT(file, file, ...) - lista de arquivos a serem linkados
  GROUP(file, file, ...) - lista de arquivos a serem linkados
  EXTERN(symbol symbol ...) - simbolo externo...
*/

/* Define o ponto de entrada e o alinhamento dos segmentos */
phys = 0x100000;
alig = 0x1000;

/* Define o formato, a arquitetura e nome do arquivo */
OUTPUT_FORMAT(binary)
OUTPUT_ARCH(i386)
OUTPUT(kernel.bin)

/* Define a funcao de entrada, que esta em kwrap.asm */
ENTRY(start)

/* Lista os arquivos a serem usados para montar o kernel (os outros sao
  passados por linha de comando) */
INPUT(kwrap.o)

/*
  Os seguintes enderecos podem ser acessados globalmente:

  - kernel_start;
  - kernel_end;
  - kernel_code;
  - kernel_data;
  - kernel_bss;
*/

SECTIONS
{

  .text phys : AT(phys)
  {
    kernel_start = .;
    kernel_code = .;

    *(.text)
    *(.rodata)

    . = ALIGN(alig);
  }

  kernel_data = .;

  .data : AT(phys + (kernel_data - kernel_start))
  {
    *(.data)
    . = ALIGN(alig);
  }

  kernel_bss = .;

  .bss : AT(phys + (kernel_bss - kernel_start))
  {
    *(.bss)
    . = ALIGN(alig);
  }

  kernel_end = .;

  /* Tudo que for associado a secao seguinte sera descartado */
  /DISCARD/ :
  {
    *(.debug_frame)
    *(.note.GNU-stack)
  }

}
