/*
============================================================================
  Este arquivo pertence ao Projeto do Sistema Operacional LuckyOS (LOS).
  --------------------------------------------------------------------------
  Copyright (C) 2013 - Luciano L. Goncalez
  --------------------------------------------------------------------------
  a.k.a.: Master Lucky
  eMail : master.lucky.br@gmail.com
  Home  : http://lucky-labs.blogspot.com.br
  ==========================================================================
  Este programa e software livre; voce pode redistribui-lo e/ou modifica-lo
  sob os termos da Licenca Publica Geral GNU, conforme publicada pela Free
  Software Foundation; na versao 2 da Licenca.

  Este programa e distribuido na expectativa de ser util, mas SEM QUALQUER
  GARANTIA; sem mesmo a garantia implicita de COMERCIALIZACAO ou de
  ADEQUACAO A QUALQUER PROPOSITO EM PARTICULAR. Consulte a Licenca Publica
  Geral GNU para obter mais detalhes.

  Voce deve ter recebido uma copia da Licenca Publica Geral GNU junto com
  este programa; se nao, escreva para a Free Software Foundation, Inc., 59
  Temple Place, Suite 330, Boston, MA 02111-1307, USA. Ou acesse o site do
  GNU e obtenha sua licenca: http://www.gnu.org/
  ==========================================================================
  bootloader_ld.cfg
  --------------------------------------------------------------------------
    Este eh um arquivo de configuracao do linkeditor, ele eh responsavel
  pela correta montagem do stage2 na sua imagem binária.
  --------------------------------------------------------------------------
  Versao: 0.1
  Data: 13/02/2018
  --------------------------------------------------------------------------
  Uso: Passado como parametro ao LD
  > ld -T bootloader_ld.cfg
  ------------------------------------------------------------------------
  Executar: Arquivo de configuracao.
  ==========================================================================
  Historico de versões
  ------------------------------------------------------------------------
  [2018-0213-1414] {v0.1} <Luciano Goncalez>

  - Implementação inicial.
============================================================================
*/



/*
  Opcoes que podem ser uteis posteriormente

  INPUT(file, file, ...) - lista de arquivos a serem linkados
  GROUP(file, file, ...) - lista de arquivos a serem linkados
  EXTERN(symbol symbol ...) - simbolo externo...
*/

/* Define o ponto de entrada e o alinhamento dos segmentos */
phys = 0x10000;
alig = 0x10;

/* Define o formato, a arquitetura e nome do arquivo */
OUTPUT_FORMAT(binary)
OUTPUT_ARCH(i386)
OUTPUT(stage2.bin)

/* Define a funcao de entrada, que esta em bwrap.asm */
ENTRY(start)

/* Lista os arquivos a serem usados para montar o bootloader
	(os outros sao passados por linha de comando) */
INPUT(bwrap.o)

/*
  Os seguintes enderecos podem ser acessados globalmente:

  - loader_start;
  - loader_end;
  - loader_code;
  - loader_data;
  - loader_bss;
*/

SECTIONS
{

  .text phys : AT(phys)
  {
    loader_start = .;
    loader_code = .;

    *(.text)

    . = ALIGN(alig);
    *(.rodata)

    . = ALIGN(alig);
  }

  loader_data = .;

  .data : AT(phys + (loader_data - loader_start))
  {
    *(.data)
    . = ALIGN(alig);
  }

  loader_bss = .;

  .bss : AT(phys + (loader_bss - loader_start))
  {
    *(.bss)
    . = ALIGN(alig);
  }

  loader_end = .;

  /* Tudo que for associado a secao seguinte sera descartado */
  /DISCARD/ :
  {
    *(.debug_frame)
    *(.note.GNU-stack)
  }

}
